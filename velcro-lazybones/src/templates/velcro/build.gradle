buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:1.1.0'
    classpath 'com.jakewharton.hugo:hugo-plugin:1.2.0'
    classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.12.0'
  }
}

// Manifest version information!
def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionBuild = 0

apply from: rootProject.file('gradle/signing.gradle')
apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'hugo'

repositories {
  jcenter()
}

dependencies {
  compile 'com.android.support:support-v4:+'
  compile 'com.android.support:support-annotations:+'

  compile 'com.inkapplications:android-preferences:+'

  compile 'com.jakewharton.timber:timber:+'
  compile 'com.jakewharton:butterknife:+'
  debugCompile 'com.jakewharton.madge:madge:+'
  debugCompile 'com.jakewharton.scalpel:scalpel:+'

  compile 'com.squareup.dagger:dagger:+'
  provided 'com.squareup.dagger:dagger-compiler:+'

  compile 'com.github.frankiesardo:icepick:+'
  provided 'com.github.frankiesardo:icepick-processor:+'

  compile fileTree(dir: 'libs', include: ['*.jar'])
}

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))

android {
  compileSdkVersion 21
  buildToolsVersion "21.1.1"

  defaultConfig {
    minSdkVersion 15
    targetSdkVersion 21

    versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
    versionName "${versionMajor}.${versionMinor}.${versionPatch}"

    buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
    buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
  }

  signingConfigs {
    release {
      storeFile file(storeFilePath)
      storePassword keystorePassword
      keyAlias storeKeyAlias
      keyPassword aliasKeyPassword
    }
  }

  buildTypes {
    debug {
      applicationIdSuffix '.dev'
      versionNameSuffix '-dev'
    }

    release {
      signingConfig signingConfigs.release
    }
  }

  lintOptions {
    abortOnError false
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }
}

apply from: rootProject.file('gradle/checkstyle.gradle')
