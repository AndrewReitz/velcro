ext {
    DEBUG = true

    /** Folder where the application we are generating the archetype is coming from */
    APP_FOLDER = "velcro-app"

    /** The resources folder inside the archetype to place the app files */
    ARCHETYPE_FOLDER = "velcro-archetype/src/main/resources/archetype-resources"

    /** App Name needs to be filtered out of files and replaced with ${applicationName} */
    FILTER_APP_NAME = "Velcro"

    /** What the application name should be replaced with. This value is defined in archetype-metadata.xml */
    APP_NAME_REPLACE = "\${applicationName}"

    /** Replaces the application name in file names with this */
    APP_NAME_FILENAME_REPLACE = "__applicationName__"

    /** Package value that needs to be filtered out and replaced with archetype variable */
    FILTER_PACKAGE = "com.andrewreitz.velcro"

    /** Standard archetype package variable */
    PACKAGE_REPLACE = "\${package}"

    /** files to be kept when doing a clean */
    KEEP_FILES = ["README.md", "pom.xml"]

    /** Any path that contains this path we need to remove it so it ends up in the java folder */
    REWRITE_PATH = "/com/andrewreitz/velcro"

    /** Directories that are now empty since we rewrote the paths of the files in them */
    REWRITE_DIRS = ["src/debug/java/com", "src/main/java/com", "src/release/java/com"]

    /** Files with these exentions have values that should be replaced with archetype variable */
    FILTER_EXTENSIONS = [".xml", ".java", ".md"]
}

defaultTasks 'clean', 'build'

task clean << {
    def files = files { file(ARCHETYPE_FOLDER).listFiles() }
    files.each { File file ->
        if (!KEEP_FILES.contains(file.name)) {
            log "Delete $file"
            delete file
        }
    }
}

task build << {
    copy {
        from APP_FOLDER
        into ARCHETYPE_FOLDER
        eachFile { file ->
            def path = file.path
            if (path.contains(REWRITE_PATH)) {
                log "Chaning path of $file"
                file.setPath(path.replace(REWRITE_PATH, ""))
            }

            def name = file.name
            if (name.contains(FILTER_APP_NAME)) {
                file.setName(name.replace(FILTER_APP_NAME, APP_NAME_FILENAME_REPLACE))
            }

            // Only filter specific files, images and binaries will become corrupt
            for (ext in FILTER_EXTENSIONS) {
                if (file.name.toLowerCase().endsWith(ext)) {
                    filter {
                        it.replace(FILTER_APP_NAME, APP_NAME_REPLACE).replace(FILTER_PACKAGE, PACKAGE_REPLACE)
                    }
                }
            }
        }
    }

    // We end up with empty directories since eachFile does not get a chance to operate on them only the files
    REWRITE_DIRS.each {
        def file = new File(ARCHETYPE_FOLDER, it)
        log "Deleting directory $file"
        delete file
    }
}

def log(message) {
    if (DEBUG) {
        println message
    }
}