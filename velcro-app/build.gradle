buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:1.3.0'
    classpath 'com.jakewharton.hugo:hugo-plugin:1.2.1'
    classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.12.0'
  }
}

// Manifest version information!
def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionBuild = 0

apply from: rootProject.file('gradle/signing.gradle')
apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.hugo'

repositories {
  jcenter()
}

dependencies {
  compile 'com.android.support:support-v4:23.1.0'
  compile 'com.android.support:support-annotations:23.1.0'
  compile 'com.android.support:appcompat-v7:23.1.0'
  compile 'com.android.support:recyclerview-v7:23.1.0'
  compile 'com.android.support:design:23.1.0'

  compile 'com.inkapplications:android-preferences:1.1.0'

  compile 'com.jakewharton.timber:timber:4.1.0'
  compile 'com.jakewharton:butterknife:7.0.1'
  debugCompile 'com.jakewharton.madge:madge:1.1.2'
  debugCompile 'com.jakewharton.scalpel:scalpel:1.1.2'

  compile 'com.squareup.dagger:dagger:1.2.2'
  provided 'com.squareup.dagger:dagger-compiler:1.2.2'

  compile 'com.github.frankiesardo:icepick:2.3.6'
  provided 'com.github.frankiesardo:icepick-processor:2.3.6'

  compile fileTree(dir: 'libs', include: ['*.jar'])
}

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))

android {
  compileSdkVersion 23
  buildToolsVersion "21.1.2"

  defaultConfig {
    minSdkVersion 16
    targetSdkVersion 23

    versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
    versionName "${versionMajor}.${versionMinor}.${versionPatch}"

    buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
    buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""

    signingConfig signingConfigs.debug
  }

  signingConfigs {
    debug {
      storeFile file('debug.keystore')
      storePassword 'android'
      keyAlias 'android'
      keyPassword 'android'
    }
    release {
      storeFile file(storeFilePath)
      storePassword keystorePassword
      keyAlias storeKeyAlias
      keyPassword aliasKeyPassword
    }
  }

  buildTypes {
    debug {
      applicationIdSuffix '.dev'
    }
    /* Uncomment this when you want to sign a release build
    release {
      signingConfig signingConfigs.release
    }
    */
  }

  productFlavors {
    internal {
      applicationId 'com.andrewreitz.velcro.internal'
    }
    production {
      applicationId 'com.andrewreitz.velcro'
    }
  }

  lintOptions {
    abortOnError false
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }
}

def installAll = tasks.create('installAll')
installAll.description = 'Install all applications.'
android.applicationVariants.all { variant ->
  installAll.dependsOn(variant.install)
  // Ensure we end up in the same group as the other install tasks.
  installAll.group = variant.install.group
}

apply from: rootProject.file('gradle/checkstyle.gradle')
